<<<<<<<< faster_romance/macro/fixme.d
	REPLACE_ACTION_TEXT_REGEXP ~\(^aerie$\)\|\(^kalah2$\)~  ~StartDialogueNoSet()~ ~StartDialogueNoSet([PC])~
	REPLACE_ACTION_TEXT ~baerie~   ~MoveGlobal("AR0607","Aerie",\[1034\.1034\],0)~ ~MoveGlobal("AR0607","Aerie",[1034.1034])~
>>>>>>>>

COMPILE ~faster_romance/macro/fixme.d~

COPY_EXISTING ~sw1h01.itm~ ~override/tb#faster1.txt~
COPY_EXISTING ~gtimes.ids~ ~override~
	READ_2DA_ENTRIES_NOW ~_#_#_#gtimes~ 2
BUT_ONLY_IF_IT_CHANGES

	DEFINE_PATCH_MACRO ~tb#romances_work~ BEGIN
		FOR (i = 0; i < _#_#_#gtimes; i+=1)
		BEGIN
			READ_2DA_ENTRY_FORMER ~_#_#_#gtimes~ i 0 number
			READ_2DA_ENTRY_FORMER ~_#_#_#gtimes~ i 1 string
			REPLACE_TEXTUALLY ~,%string%)~ ~,%number%)~
		END
		REPLACE_EVALUATE ~RealSetGlobalTimer(\(\"[^"]*\",\"[^"]*\"\),\([^)]*\))~
		BEGIN
			temp = multiply * MATCH2 ** (exponentiation nth_root) / divide
			MATCH2 = temp < MATCH2 ? temp : MATCH2
			SPRINT RESULT ~%MATCH2%~
		END
		~RealSetGlobalTimer(%MATCH1%,%RESULT%)~
	END

COPY_EXISTING_REGEXP GLOB ~.*\.bcs~ ~override~
DECOMPILE_BCS_TO_BAF
	LAUNCH_PATCH_MACRO ~tb#romances_work~
COMPILE_BAF_TO_BCS
BUT_ONLY_IF_IT_CHANGES
IF ~^268OB~

COPY_EXISTING_REGEXP GLOB ~.*\.dlg~ ~override~
DECOMPILE_DLG_TO_D
	LAUNCH_PATCH_MACRO ~tb#romances_work~
COMPILE_D_TO_DLG
IF ~RealSetGlobalTimer~
BUT_ONLY_IF_IT_CHANGES
